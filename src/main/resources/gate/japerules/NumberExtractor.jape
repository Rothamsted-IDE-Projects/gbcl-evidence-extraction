Phase:   NumberExtractor
Input: Token Lookup
Options: control = appelt

// Source ANNIE NE Transducer number.jape

Macro: MILLION_BILLION
//note: now includes T for trillion
({Token.string == "m"}|
{Token.string == "million"}|
{Token.string == "millions"}|
{Token.string == "Million"}|
{Token.string == "Millions"}|
{Token.string == "b"}|
{Token.string == "billion"}|
{Token.string == "billions"}|
{Token.string == "Billion"}|
{Token.string == "Billions"}|
{Token.string == "bn"}|
{Token.string == "k"}|
{Token.string == "K"}|
{Token.string == "T"}
)

Macro: NUMBER_WORDS
// two hundred and thirty five
// twenty five

(
 (({Lookup.majorType == number}
   ({Token.string == "-"})?
  )*
   {Lookup.majorType == number}
   {Token.string == "and"}
 )*
 ({Lookup.majorType == number}
  ({Token.string == "-"})?
 )*
   {Lookup.majorType == number}
)

Macro: AMOUNT_NUMBER
// enables commas, decimal points and million/billion
// to be included in a number

(({Token.kind == number}
  (({Token.string == ","}|
    {Token.string == "."}
   )
   {Token.kind == number}
  )*
  |
  (NUMBER_WORDS)
 )
 (MILLION_BILLION)?
)

/**
  Annotate number as Number.
*/
Rule:NumberLookupRule
(
  { Token.kind == "number" }
):tag
-->
:tag.Number = {kind = "number", rule = "NumberLookupRule"}


// Source: ANNIE NE Transducer number.jape
Rule:   NumberExtractorRule
// 30 pounds
  (
      (AMOUNT_NUMBER)
  )
:number -->
  :number.ImpactNumber = {kind = "number", rule = "NumberExtractorRule"}


